apply plugin: "org.springframework.boot"
//apply plugin: "application"

configurations {
    agentBootJar
}

license {
    exclude "*.yml"
    exclude "*.xml"
    exclude "*.txt"
}

dependencies {
    /*******************************
     * Implementation Dependencies
     *******************************/

    implementation(project(":genie-agent"))
    implementation("org.springframework.boot:spring-boot-starter")

    /*******************************
     * Compile Only Dependencies
     *******************************/

    /*******************************
     * Runtime Only Dependencies
     *******************************/

    /*******************************
     * Test Dependencies
     *******************************/

    testImplementation(project(":genie-test"))
}

springBoot {
    mainClassName = "com.netflix.genie.GenieAgentApplication"
}

def dockerDir = "${project.projectDir}/src/main/docker"
def genieAgentAppImageName = "genie-agent-app"

task dockerBuildAgentAppImage(type: Exec, group: "Docker", description: "") {
    onlyIf {
        rootProject.ext.isDockerRunning()
    }
    inputs.dir(dockerDir)
    workingDir dockerDir

    def commandArgs = new ArrayList<>()
    commandArgs.add("docker")
    commandArgs.add("build")
    commandArgs.add("--force-rm")
    for (String tag : rootProject.ext.getDockerTags(genieAgentAppImageName, project.version.toString())) {
        commandArgs.add("-t")
        commandArgs.add(tag)
    }
    commandArgs.add(".")

    commandLine commandArgs
}

//mainClassName = "com.netflix.genie.GenieAgentApplication"
//applicationDefaultJvmArgs = ["-noverify", "-XX:TieredStopAtLevel=1"]

def genieVersion = project.version.toString()

jar {
    manifest {
        attributes("Implementation-Version": genieVersion)
    }
}

// Used to provide convenient way to reference the actual executable jar between projects
artifacts {
    agentBootJar bootJar
}

bootBuildImage {
    builder = "paketobuildpacks/builder@sha256:3c5f88e55da475957bd03b4d6e852e0938bb73ee1a5184ae291a7566ca83d97f"
    runImage = "paketobuildpacks/run@sha256:562ddd10160a2629168a0108894766b46dff7f4b0db263ecabe35cd56fcbe56d"
}
